#использовать "../src"
#Использовать asserts
#Использовать logos

Перем юТест;
Перем ЭтоWindows;

Функция ПолучитьСписокТестов(Знач Тестирование) Экспорт

	юТест = Тестирование;

	ИменаТестов = Новый Массив;
	ИменаТестов.Добавить("ТестДолжен_ПроверитьПлатформа1С_Windows");
	ИменаТестов.Добавить("ТестДолжен_ПроверитьПлатформа1С_OSX");
	ИменаТестов.Добавить("ТестДолжен_ПроверитьПлатформа1С_Linux");

	Возврат ИменаТестов;

КонецФункции

Процедура ТестДолжен_ПроверитьПлатформа1С_Windows() Экспорт
	
	Если Не ЭтоWindows Тогда
		Сообщить("Тест выполняется только на windows");
		юТест.ТестПройден();
		Возврат;
	КонецЕсли;

	КаталогFixtures = ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures");
	КаталогWindows = ОбъединитьПути(КаталогFixtures, "windows");
	Платформа1С = Новый Платформа1С;
	Платформа1С.СброситьПоиск();
	Платформа1С.ДобавитьКаталоГПоиска(КаталогWindows, РазрядностьПлатформы.x86);

	ПутьКПредприятию = Платформа1С.ПутьКПредприятию("8.3.13.9999");

	Ожидаем.Что(НЕ ПустаяСтрока(ПутьКПредприятию), "исполняемый файл приложения 1С найден").ЭтоИстина();

КонецПроцедуры

Процедура ТестДолжен_ПроверитьПлатформа1С_OSX() Экспорт

	Если ЭтоWindows Тогда
		Сообщить("Тест выполняется только на unix системых");
		юТест.ТестПройден();
		Возврат;
	КонецЕсли;

	КаталогFixtures = ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures");
	КаталогВерсий = ОбъединитьПути(КаталогFixtures, "linux", "osx");
	Платформа1С = Новый Платформа1С;
	Платформа1С.СброситьПоиск();
	Платформа1С.ДобавитьКаталоГПоиска(КаталогВерсий, РазрядностьПлатформы.x64);

	ПутьКПредприятию = Платформа1С.ПутьКПредприятию("8.3.13.9999");
	ЭталонныйПуть = ОбъединитьПути(КаталогВерсий, "8.3.13.9999", "1cv8");
	Ожидаем.Что(ПутьКПредприятию, "исполняемый файл приложения 1С найден").Равно(ЭталонныйПуть);

КонецПроцедуры

Процедура ТестДолжен_ПроверитьПлатформа1С_Linux() Экспорт

	Если ЭтоWindows Тогда
		Сообщить("Тест выполняется только на unix системых");
		юТест.ТестПройден();
		Возврат;
	КонецЕсли;

	КаталогFixtures = ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures");
	КаталогВерсий_x86 = ОбъединитьПути(КаталогFixtures, "linux", "8.3.13.9999", "i386");
	КаталогВерсий_x64 = ОбъединитьПути(КаталогFixtures, "linux", "8.3.13.9999", "x86_64");

	Платформа1С = Новый Платформа1С;
	Платформа1С.СброситьПоиск();
	Платформа1С.ДобавитьКаталоГПоиска(КаталогВерсий_x86, РазрядностьПлатформы.x86, "0.0.0.0");
	Платформа1С.ДобавитьКаталоГПоиска(КаталогВерсий_x64, РазрядностьПлатформы.x64, "0.0.0.0");

	КаталогВерсий_x86_8 = ОбъединитьПути(КаталогFixtures, "linux", "8.3.13.9998", "i386");
	КаталогВерсий_x64_8 = ОбъединитьПути(КаталогFixtures, "linux", "8.3.13.9998", "x86_64");

	Платформа1С.ДобавитьКаталоГПоиска(КаталогВерсий_x86, РазрядностьПлатформы.x86, "0.0.0.0");
	Платформа1С.ДобавитьКаталоГПоиска(КаталогВерсий_x64, РазрядностьПлатформы.x64, "0.0.0.0");

	ПутьКПредприятию_x86 = Платформа1С.ПутьКПредприятию("8.3.13", РазрядностьПлатформы.x86);
	ПутьКПредприятию_x64 = Платформа1С.ПутьКПредприятию("8.3.13", РазрядностьПлатформы.x64);
	ПутьКПредприятию_x86x64 = Платформа1С.ПутьКПредприятию("8.3.13.9999", РазрядностьПлатформы.x86x64);
	ПутьКПредприятию_x64x86 = Платформа1С.ПутьКПредприятию("8.3.13.9999", РазрядностьПлатформы.x64x86);
	
	ЭталонныйПуть = ОбъединитьПути(КаталогВерсий_x86, "1cv8");
	Ожидаем.Что(ПутьКПредприятию_x86, "ПутьКПредприятию_x86 исполняемый файл приложения 1С найден").Равно(ЭталонныйПуть);

	ЭталонныйПуть = ОбъединитьПути(КаталогВерсий_x64, "1cv8");
	Ожидаем.Что(ПутьКПредприятию_x64, "ПутьКПредприятию_x64 исполняемый файл приложения 1С найден").Равно(ЭталонныйПуть);

	ЭталонныйПуть = ОбъединитьПути(КаталогВерсий_x86, "1cv8");
	Ожидаем.Что(ПутьКПредприятию_x86x64, "ПутьКПредприятию_x86x64 исполняемый файл приложения 1С найден").Равно(ЭталонныйПуть);

	ЭталонныйПуть = ОбъединитьПути(КаталогВерсий_x64, "1cv8");
	Ожидаем.Что(ПутьКПредприятию_x64x86, "ПутьКПредприятию_x64x86 исполняемый файл приложения 1С найден").Равно(ЭталонныйПуть);

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("oscript.lib.v8find");
Лог.УстановитьУровень(УровниЛога.Отладка);

СИ = Новый СистемнаяИнформация;
ЭтоWindows = Найти(НРег(СИ.ВерсияОС), "windows") > 0;




